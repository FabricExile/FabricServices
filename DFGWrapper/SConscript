#
# Copyright 2010-2015 Fabric Software Inc. All rights reserved.
#

Import('parentEnv', 'capiIncludeDir', 'stageDir')

env = parentEnv.CloneSubStage('DFGWrapper')

env.Append(CPPDEFINES = ['FEC_SHARED'])
env.Append(CPPPATH = [capiIncludeDir])

sources = Glob('*.cpp')

dfgWrapperLib = env.StaticLibrary('Fabric-DFGWrapper', sources)

dfgWrapperIncludeDir = env.Dir('.').dir.srcnode()

dfgWrapperFlags = {
  'CPPPATH': [dfgWrapperIncludeDir],
  'LIBS': [dfgWrapperLib]
  }

installedDfgWrapperHeaders = env.Install(stageDir.Dir('include').Dir('DFGWrapper'), env.Glob('*.h'))
installedDfgWrapperLib = env.Install(stageDir.Dir('lib'), dfgWrapperLib)

Export('dfgWrapperLib', 'dfgWrapperIncludeDir', 'dfgWrapperFlags')
Alias('dfgWrapper', dfgWrapperLib + installedDfgWrapperHeaders + installedDfgWrapperLib)
Return('dfgWrapperLib')
